plugins {
    id "java"
    id "org.springframework.boot" version "3.3.0"
    id "io.spring.dependency-management" version "1.1.5"
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
    id "com.diffplug.spotless" version "6.25.0"
}

group = "com.sivalabs"
version = "0.0.1-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}
ext {
    set("springdoc_version", "2.5.0")
    set("commons_io_version", "2.16.1")
    set("opencsv_version", "5.9")
    set("jjwt_version", "0.12.6")
    set("instancio_version", "4.7.0")
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"

    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "io.jsonwebtoken:jjwt-api:${jjwt_version}"
    implementation "io.jsonwebtoken:jjwt-impl:${jjwt_version}"
    implementation "io.jsonwebtoken:jjwt-jackson:${jjwt_version}"

    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-database-postgresql"
    compileOnly "org.projectlombok:lombok"
    testImplementation "org.projectlombok:lombok"
    testImplementation "org.springframework.boot:spring-boot-devtools"
    runtimeOnly "org.postgresql:postgresql"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor "org.projectlombok:lombok"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdoc_version}"
    implementation "com.opencsv:opencsv:${opencsv_version}"
    implementation "org.apache.commons:commons-lang3"
    implementation "commons-io:commons-io:${commons_io_version}"

    developmentOnly "org.springframework.boot:spring-boot-docker-compose"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:postgresql"
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "org.instancio:instancio-junit:${instancio_version}"
}

bootBuildImage {
    imageName = "sivaprasadreddy/devzone-api-springboot"
}

gitProperties {
    failOnNoGitDirectory = false
    keys = ["git.branch",
            "git.commit.id.abbrev",
            "git.commit.user.name",
            "git.commit.message.full"
    ]
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        palantirJavaFormat("2.39.0")
        formatAnnotations()
    }
}

tasks.named("test", Test) {
    useJUnitPlatform()
    testLogging {
        events = ["PASSED", "FAILED", "SKIPPED"]
        //showStandardStreams = true
        //exceptionFormat = "full"
    }
}
